//========================================================
//	RoSF.us Development Team Presents
//	 ______           ______  ______ 
//	|  __  |  ______ |  ____||  ____|     _    _   _ __ _ 
//	| |__/ / |  __  || |____ | |___      | |  | | |  ____|
//	|  __ _  | |  | ||____  ||  ___|     | |  | | |_|__ _ 
//	| |  | | | |__| | ____| || |      _  | |__| |  ____| |
//	|_|  |_| |______||______||_|     |_| |______| |______|
//========================================================
// 职业/综合NPC

prontera,146,92,6	script	孙燕姿 玩家辅助指导	4_F_SON,{
function Get_Platinum; function Equip_Menu; function Cm;

	if (getarraysize(.OnClick)) {
		if (.OnClick[0]) {
			specialeffect2 EF_HEAL;
			percentheal 100,100;
		}
		if (.OnClick[1]) {
			skilleffect "AL_BLESSING",0; sc_start SC_BLESSING,120000,5;;
			skilleffect "AL_INCAGI",0; sc_start SC_INCREASEAGI,140000,5;
		}
		if (.OnClick[2])
			repairall;
	}
	cutin "son",2;
	mes .n$;
	mes "---------------------------------";
	set .@time, gettime(3);
	mes "唔! "+((.@time < 5 || .@time > 17)?"晚上好":((.@time < 12)?"早上好":"下午好"))+", "+strcharinfo(0)+"!";
	mes "我能为您做什么?";
	emotion e_ho;
	next;
	disable_items;
	switch(select(.Menu$)) {
	case 1:
		function Job_Menu; function A_An;
		mes .n$;
		mes "---------------------------------";
		mes "您确定需要使用快速转职系统?";
		mes "完成二转, 三转和转生任务的职业奖励 与便捷转职所获得奖励是有很大区别的. 不过前提是您必须毕业于初心者训练营.";
		if(select("- 我要继续使用快速转职系统.:- 我按错了,拜拜.") == 2){
			next;
			mes .n$;
			mes "---------------------------------";
			mes F_Bye;
			cutin "",255; 
			close;
			end;
			}
		next;
		mes .n$;
		mes "---------------------------------";
		if (Class > 4049) {
			mes "您已经无法再转职了.";
			cutin "",255; close;
		}
		if (checkfalcon() || checkcart() || checkriding() || ismounting()) {
			mes "请移除 "+((checkfalcon())?"猎鹰":"")+((checkcart())?"手推车":"")+((checkriding())?"大嘴鸟":"")+((ismounting())?"坐骑":"")+" 后再继续.";
			cutin "",255; close;
		}
		if (.SkillPointCheck && SkillPoint) {
			mes "请在转职仪式之前用完您的技能点.";
			cutin "",255; close;
		}
		set .@eac, eaclass();
		set .@i, ((.ThirdClass)?roclass(.@eac&EAJ_UPPERMASK):Class);
		if (.@i > 6 && .@i < 22) {
			if (BaseLevel < .Rebirth[0] || JobLevel < .Rebirth[1]) {
				set .@blvl, .Rebirth[0]-BaseLevel; set .@jlvl, .Rebirth[1]-JobLevel;
				mes "您需要再升 "+((.@blvl>0)?.@blvl+" 级基本等级 "+((.@jlvl>0)?"/ ":""):"")+((.@jlvl>0)?.@jlvl+" 级职业等级 ":"")+"才能进行转生.";
				cutin "",255; close;
			}
			if (Class > 21) {
				mes "转职成为三转职业?";
				next;
				Job_Menu(roclass(.@eac|EAJL_THIRD));
				cutin "",255; close;
			}
			while(1) {
				mes "选择对应菜单.";
				next;
				set .@i, select("- ^0055FF转生^000000:"+((.ThirdClass)?"- ^FF0000三转职业^000000":"")+":- ^777777取消^000000");
				if (.@i==3){cutin "",255; close;}
				mes .n$;
				mes "---------------------------------";
				mes "我想您最好不要搞错, 这个操作不可逆转的!";
				next;
				Job_Menu(((.@i==1)?4001:roclass(.@eac|EAJL_THIRD)));
				mes .n$;
				mes "---------------------------------";
			}
		}
		set .@j1, roclass(.@eac|EAJL_2_1); set .@j2,roclass(.@eac|EAJL_2_2);
		if ((.@eac&EAJ_UPPERMASK) == EAJ_SUPER_NOVICE) setarray .@exp[0],roclass(.@eac|EAJL_THIRD),99;
		if ((Class == Job_Ninja)||(Class == Job_Gunslinger)) setarray .@exp[0],.@j1,70;
		if (.@exp[0] && .ThirdClass) {
			if (BaseLevel < .Rebirth[0] || JobLevel < .@exp[1]) {
				set .@blvl, .Rebirth[0]-BaseLevel; set .@jlvl, .@exp[1]-JobLevel;
				mes "您需要再升 "+((.@blvl>0)?.@blvl+" 级基本等级 "+((.@jlvl>0)?"/ ":""):"")+((.@jlvl>0)?.@jlvl+" 级职业等级 ":"")+"才能进行转职.";
				cutin "",255; close;
			}
			mes "转职成为 "+jobname(.@exp[0])+"?";
			next;
			Job_Menu(.@exp[0]);
			cutin "",255; close;
		}
		if (.@eac&EAJL_2)
			if (.@eac&(EAJL_UPPER|EAJL_BABY) || roclass(.@eac|EAJL_UPPER) == -1) {
				mes "您已经无法再转职了.";
				cutin "",255; close;
			}
		if ((.@eac&EAJ_BASEMASK) == EAJ_NOVICE) {
			if (JobLevel < .JobReq[0])
				mes "职业等级至少要高于 "+.JobReq[0]+" 才能进行第一次转职.";
			else if (Class == 4001 && .LastJob && lastJob) {
				mes "进行转职?";
				next;
				Job_Menu(roclass((eaclass(lastJob)&EAJ_BASEMASK)|EAJL_UPPER));
			} else switch(Class) {
				case 0: Job_Menu(1,2,3,4,5,6,23,4046,24,25,4023);
				case 4001: Job_Menu(4002,4003,4004,4005,4006,4007);
				case 4023: Job_Menu(4024,4025,4026,4027,4028,4029,4045);
				default: mes "发生错误, 请联系GM."; break;
			}
			cutin "",255; close;
		}
		if (roclass(.@eac|EAJL_2_1) == -1 || roclass(.@eac|EAJL_2_2) == -1)
			mes "您已经无法再转职了.";
		else if (!(.@eac&EAJL_2) && JobLevel < .JobReq[1])
			mes "职业等级至少要高于 "+.JobReq[1]+" 才能转职为二转职业.";
		else if (.LastJob && lastJob && (.@eac&EAJL_UPPER)) {
			mes "进行转职?";
			next;
			Job_Menu(lastJob+4001);
		} else
			Job_Menu(.@j1,.@j2);
		cutin "",255; close;
		function Job_Menu {
			while(1) {
				if (getargcount() > 1) {
					mes "选择一种职业.";
					set .@menu$,"";
					for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
						set .@menu$, .@menu$+"- "+jobname(getarg(.@i))+":";
					set .@menu$, .@menu$+"- ^777777取消^000000";
					next;
					set .@i, getarg(select(.@menu$)-1,0);
					if (!.@i){cutin "",255; close;}
					if ((.@i == 23 || .@i == 4045) && BaseLevel < .SNovice) {
						mes .n$;
						mes "---------------------------------";
						mes "只有初始职业为 "+.SNovice+" 才能转职成为 "+jobname(.@i)+".";
						cutin "",255; close;
					}
					mes .n$;
					mes "---------------------------------";
					mes "您确认吗?";
					next;
				} else
					set .@i, getarg(0);
				if (select("- 转职成为 ^0055FF"+jobname(.@i)+"^000000:- ^777777"+((getargcount() > 1)?"返回":"取消")+"^000000") == 1) {
					mes .n$;
					mes "---------------------------------";
					mes "您已经成为 "+A_An(jobname(.@i))+"!";
					if (.@i==4001 && .LastJob) set lastJob, Class;
					jobchange .@i;
					if (.@i==4001 || .@i==4023)
						resetlvl(1);
					specialeffect2 EF_ANGEL2;
					specialeffect2 EF_ELECTRIC;
					if (.Platinum)
						Get_Platinum;
					cutin "",255; close;
				}
				if (getargcount() == 1) return;
				mes .n$;
				mes "---------------------------------";
			}
			end;
		}
		function Get_Platinum {
			skill 142,1,0;
			switch(BaseClass) {
				case 0: if (Class !=23) skill 143,1,0; break;
				case 1: skill 144,1,0; skill 145,1,0; skill 146,1,0; break;
				case 2: skill 157,1,0; break;
				case 3: skill 147,1,0; skill 148,1,0; break;
				case 4: skill 156,1,0; break;
				case 5: skill 153,1,0; skill 154,1,0; skill 155,1,0; break;
				case 6: skill 149,1,0; skill 150,1,0; skill 151,1,0; skill 152,1,0; break;
				default: break;
			}
			switch(BaseJob) {
				case 7: skill 1001,1,0; break;
				case 8: skill 1014,1,0; break;
				case 9: skill 1006,1,0; break;
				case 10: skill 1012,1,0; skill 1013,1,0; break;
				case 11: skill 1009,1,0; break;
				case 12: skill 1003,1,0; skill 1004,1,0; break;
				case 14: skill 1002,1,0; break;
				case 15: skill 1015,1,0; skill 1016,1,0; break;
				case 16: skill 1007,1,0; skill 1008,1,0; skill 1017,1,0; skill 1018,1,0; skill 1019,1,0; break;
				case 17: skill 1005,1,0; break;
				case 18: skill 238,1,0; break;
				case 19: skill 1010,1,0; break;
				case 20: skill 1011,1,0; break;
				default: break;
			}
			return;
		}
		function A_An {
			setarray .@A$[0],"a","e","i","o","u";
			set .@B$, "_"+getarg(0);
			for(set .@i,0; .@i<5; set .@i,.@i+1)
				if (compare(.@B$,"_"+.@A$[.@i])) return "一名 "+getarg(0);
			return "一名 "+getarg(0);
		}
	case 2:
		mes .n$;
		mes "---------------------------------";
		mes "请选择下面的菜单.";
		next;
		setarray .@Look[1],7,1,6;
		set .@s, select("- 衣服颜色:- 发型:- 头发颜色");
		set .@Revert, getlook(.@Look[.@s]);
		set .@Style,1;
		mes .n$;
		mes "---------------------------------";
		mes "这个造型如何?";
		while(1) {
			setlook .@Look[.@s], .@Style;
			message strcharinfo(0),"这是样式 #"+.@Style+".";
			set .@menu$, "- 下一个 (^0055FF"+((.@Style!=.Styles[.@s])?.@Style+1:1)+"^000000):- 上一个 (^0055FF"+((.@Style!=1)?.@Style-1:.Styles[.@s])+"^000000):- 跳转至...:- 恢复到原来的 (^0055FF"+.@Revert+"^000000)";
			switch(select(.@menu$)) {
			case 1:
				set .@Style, ((.@Style!=.Styles[.@s])?.@Style+1:1);
				break;
			case 2:
				set .@Style, ((.@Style!=1)?.@Style-1:.Styles[.@s]);
				break;
			case 3:
				next;
				mes .n$;
				mes "---------------------------------";
				mes "选择一个样式编号在 1 - "+.Styles[.@s]+".";
				input .@Style,0,.Styles[.@s];
				if (!.@Style)
					set .@Style, rand(1,.Styles[.@s]);
				next;
				mes .n$;
				mes "---------------------------------";
				mes "这个造型如何?";
				break;
			case 4:
				set .@Style, .@Revert;
				setlook .@Look[.@s], .@Revert;
				break;
			}
		}
		cutin "",255; close;
	case 3:
		function Refine_Item;
		if (.EnrichedRefine) {
			mes .n$;
			mes "select an option.";
			next;
			if (select("- Normal Refiner:- Enriched Refiner") == 2) {
				mes .n$;
				mes "What do you want to refine?";
				next;
				Equip_Menu(1);
				setarray .@Materials[0],7619,7620,7620,7620,7620;
				setarray .@Safe[0],4,7,6,5,4;
				set .@WLv, getequipweaponlv(@i);
				mes .n$;
				mes "Item: +"+getequiprefinerycnt(@i)+" "+getequipname(@i);
				next;
				if(select("- Refine item (2 * ^0055FF"+getequippercentrefinery(@i)+"^000000% success):- ^777777Cancel^000000") == 2)
					cutin "",255; close;
				Refine_Item(1, .@Materials[.@WLv], .RP[.@WLv], .@Safe[.@WLv], 1);
				cutin "",255; close;
			}
		}
		mes .n$;
		mes "What do you want to refine?";
		next;
		Equip_Menu(1);
		setarray .@Materials[0],985,1010,1011,984,984;
		setarray .@Safe[0],4,7,6,5,4;
		set .@WLv, getequipweaponlv(@i);
		set .@SafeCount, .@Safe[.@WLv]-getequiprefinerycnt(@i);
		mes .n$;
		mes "Item: +"+getequiprefinerycnt(@i)+" "+getequipname(@i);
		next;
		switch(select("- Refine once (^0055FF"+getequippercentrefinery(@i)+"^000000% success):- Refine multiple times (up to "+(10-getequiprefinerycnt(@i))+"):- Refine to safe limit ("+((.@SafeCount>0)?.@SafeCount+" refines":"^777777disabled^000000")+"):- ^777777Cancel^000000")) {
		case 1:
			Refine_Item(1, .@Materials[.@WLv], .RP[.@WLv], .@Safe[.@WLv]);
			cutin "",255; close;
		case 2:
			input .@Refines;
			if (.@Refines+getequiprefinerycnt(@i) > 10 || .@Refines < 1) {
				mes .n$;
				mes "Invalid number ("+.@Refines+").";
			} else
				Refine_Item(.@Refines, .@Materials[.@WLv], .RP[.@WLv], .@Safe[.@WLv]);
			cutin "",255; close;
		case 3:
			if (.@SafeCount < 1) {
				mes .n$;
				mes "The item has already reached the safe limit.";
			} else
				Refine_Item(.@SafeCount, .@Materials[.@WLv], .RP[.@WLv], .@Safe[.@WLv]);
			cutin "",255; close;
		case 4:
			cutin "",255; close;
		}
		function Equip_Menu {
			setarray .@pos$[1],"Upper Headgear","Armor","Left hand","Right hand","Garment","Footgear","Left Accessory","Right Accessory","Mid Headgear","Lower Headgear";
			set .@menu$,"";
			for (set .@i,1; .@i <= 10; set .@i,.@i+1) {
				if (getequipisequiped(.@i))
					set .@menu$, .@menu$+.@pos$[.@i]+" [^0055FF"+getequipname(.@i)+"^000000]";
				set .@menu$, .@menu$+":";
			}
			set @i, select(.@menu$);
			if (!getequipisequiped(@i)) {
				mes .n$;
				mes "Nothing is equipped there!";
				cutin "",255; close;
			}
			if (getarg(0)) {
				if (!getequipisenableref(@i)) {
					mes .n$;
					mes getequipname(@i)+" cannot be refined.";
					cutin "",255; close;
				}
				if (getequiprefinerycnt(@i) >= 10 ) {
					mes .n$;
					mes "+"+getequiprefinerycnt(@i)+" "+getequipname(@i)+" cannot be refined any further.";
					cutin "",255; close;
				}
			}
			return;
		}
		function Refine_Item {
			mes .n$;
			set .@price, getarg(0)*getarg(2);
			mes "I'll need "+getarg(0)+"x "+getitemname(getarg(1))+" and "+Cm(.@price)+" Zeny.";
			if (countitem(getarg(1)) < getarg(0) || Zeny < .@price) {
				mes "Come back when you have the required materials.";
				cutin "",255; close;
			}
			if (getequiprefinerycnt(@i)+getarg(0) > getarg(3))
				mes "The item will be refined above the safe limit. It may break.";
			next;
			if(select("- ^0055FFContinue^000000:- ^777777Cancel^000000") == 2)
				cutin "",255; close;
			mes .n$;
			set .@j, getarg(0);
			while(.@j) {
				delitem getarg(1),1;
				set Zeny, Zeny-getarg(2);
				if (getarg(4,0)) {
					if (getequippercentrefinery(@i) <= rand(100) && getequippercentrefinery(@i) <= rand(100)) {
						mes getequipname(@i)+" broke while refining.";
						failedrefitem @i;
						cutin "",255; close;
					}
				} else {
					if (getequippercentrefinery(@i) <= rand(100)) {
						mes getequipname(@i)+" broke while refining.";
						failedrefitem @i;
						cutin "",255; close;
					}
				}
				successrefitem @i;
				set .@j, .@j-1;
			}
			mes "All done!";
			cutin "",255; close;
		}
		function Cm {
			set .@str$, getarg(0);
			for(set .@i,getstrlen(.@str$)-3; .@i>0; set .@i,.@i-3)
				set .@str$, insertchar(.@str$,",",.@i);
			return .@str$;
		}
	case 4:
		mes .n$;
		mes "---------------------------------";
		if (.Prices[0])
			mes "100%安全的移除卡片需要 "+Cm(.Prices[0])+" Zeny.";
		mes "请选择需要拆除卡片的装备.";
		next;
		Equip_Menu(0);
		mes .n$;
		mes "---------------------------------";
		if (Zeny < .Prices[0]) {
			mes "您的Zeny不够.";
			cutin "",255; close;
		}
		set .@j,getequipcardcnt(@i);
		if (!.@j) {
			mes "您所选择的 "+getequipname(@i)+" 上没有卡片可以拆除.";
			cutin "",255; close;
		}
		if (!checkweight(4001,(.@j+1))) {
			mes "您携带的物品太多了!";
			cutin "",255; close;
		}
		mes "装备: "+getequipname(@i);
		for(set .@i,0; .@i<.@j; set .@i,.@i+1)
			mes "卡片 "+(.@i+1)+": "+getitemname(getequipcardid(@i,.@i));
		next;
		if (select("- 拆除 "+.@j+" 卡片(s):- ^777777取消^000000") == 2){
			cutin "",255; close;}
		set Zeny, Zeny-.Prices[0];
		successremovecards @i;
		mes .n$;
		mes "---------------------------------";
		mes "卡片已成功拆除!";
		cutin "",255; close;
	case 5:
		mes .n$;
		mes "---------------------------------";
		mes "每个硬币需要 "+Cm(.Coin[1])+" Zeny.";
		next;
		switch(select("- Zeny 换硬币:- 硬币换 Zeny:- 关闭")) {
		case 1:
			mes .n$;
			mes "---------------------------------";
			mes "您需要兑换多少硬币?";
			next;
			input .@coins,0,(1000000000/.Coin[1]);
			mes .n$;
			mes "---------------------------------";
			if (.@coins*.Coin[1] > Zeny) {
				mes "您的Zeny不够.";
				cutin "",255; close;
			}
			if (!checkweight(.Coin[0],.@coins)) {
				mes "您的背包空间不足.";
				cutin "",255; close;
			}
			set Zeny, Zeny-(.Coin[1]*.@coins);
			getitem .Coin[0], .@coins;
			mes "购买了 "+Cm(.@coins)+" 硬币.";
			cutin "",255; close;
		case 2:
			mes .n$;
			mes "---------------------------------";
			mes "您装备出售多少硬币?";
			next;
			input .@coins,0,(1000000000/.Coin[1]);
			mes .n$;
			mes "---------------------------------";
			if (countitem(.Coin[0]) < .@coins) {
				mes "您没有足够的硬币!";
				cutin "",255; close;
			}
			if ((Zeny+(.Coin[1]*.@coins)) > 1000000000) {
				mes "您的Zeny所持到上限了!";
				cutin "",255; close;
			}
			delitem .Coin[0], .@coins;
			set Zeny, Zeny+(.Coin[1]*.@coins);
			mes "兑换了 "+Cm(.@coins)+" 硬币.";
			cutin "",255; close;
		case 3:
			cutin "",255; close;
		}
	case 6:
		if (Zeny < .Prices[2]) {
			mes .n$;
			mes "---------------------------------";
			mes "物品租赁需要 "+Cm(.Prices[2])+" Zeny.";
			cutin "",255; close;
		}
		if (ismounting()) {
			mes .n$;
			mes "---------------------------------";
			mes "您必须要先移除您的坐骑.";
			cutin "",255; close;
		} else if ((eaclass()&EAJ_THIRDMASK==EAJ_RANGER) && !countitem(6124)) {
			if (!checkfalcon() && getskilllv("HT_FALCON") && !checkoption(Option_Wug) && !checkoption(Option_Wugrider)) {
				mes .n$;
				mes "---------------------------------";
				mes "请选择下列菜单中的内容.";
				next;
				if(select("- 猎鹰:- 座狼") == 1) setfalcon;
				else getitem 6124,1;
			} else getitem 6124,1;
		} else if ((eaclass()&EAJ_THIRDMASK==EAJ_MECHANIC) && !checkcart() && getskilllv("MC_PUSHCART")) {
			if (!checkmadogear() && getskilllv("NC_MADOLICENCE")) {
				mes .n$;
				mes "---------------------------------";
				mes "请选择下列菜单中的内容.";
				next;
				if(select("- 手推车:- 座驾[魔能机甲/坐骑龙]")==1) setcart;
				else setmadogear;
			} else setcart;
		} else if (!checkcart() && getskilllv("MC_PUSHCART")) setcart;
		else if (!checkfalcon() && getskilllv("HT_FALCON") && !checkoption(Option_Wug) && !checkoption(Option_Wugrider)) setfalcon;
		else if (!checkriding() && getskilllv("KN_RIDING")) {
			if (eaclass()&EAJ_THIRDMASK == EAJ_RUNE_KNIGHT) setdragon;
			else setriding;
		} else if (!checkmadogear() && getskilllv("NC_MADOLICENCE")) setmadogear;
		else {
			mes .n$;
			mes "---------------------------------";
			mes "您看上去没有东西可以租赁.";
			cutin "",255; close;
		}
		set Zeny, Zeny-.Prices[2];
		specialeffect2 EF_TEIHIT3;
		mes .n$;
		mes "---------------------------------";
		mes "请保管好您的租赁物品.";
		emotion e_no1;
		cutin "",255; close;
	case 7:
		mes .n$;
		mes "---------------------------------";
		if (.Prices[1])
			mes "需要 "+(.Prices[1])+" Zeny, 来进行技能或者属性点重置.";
		mes "请选择对应的菜单.";
		next;
		set .@i, select("- 重置属性:- 重置技能:- 取消");
		if (.@i == 3)
			{cutin "",255; close;}
		mes .n$;
		mes "---------------------------------";
		if (Zeny < .Prices[1]) {
			mes "您的Zeny不够.";
			cutin "",255; close;
		}
		set Zeny, Zeny-.Prices[1];
		if (.@i == 1) {
			resetstatus;
			specialeffect2 EF_CHEMICALBODY;
			mes "属性已重置!";
		} else {
			resetskill;
			specialeffect2 EF_ELECTRIC4;
			mes "技能已重置!";
		}
		cutin "",255; close;
	case 8:
		Get_Platinum;
		specialeffect2 EF_BLUECASTING;
		mes .n$;
		mes "---------------------------------";
		mes "技能已修复完成!";
		cutin "",255; close;
	case 9:
		set .@Var$, ".SexChange"+((Sex)?"F":"M");
		if (getarraysize(getd(.@Var$))) {
			mes .n$;
			mes "---------------------------------";
			mes "要变更成为 "+((Sex)?"女性":"男性")+" 您需要准备下列物品:";
			for(set .@i,0; .@i<getarraysize(getd(.@Var$)); set .@i,.@i+2) {
				mes "- "+getd(.@Var$+"["+(.@i+1)+"]")+"x "+getitemname(getd(.@Var$+"["+.@i+"]"));
				if (countitem(getd(.@Var$+"["+.@i+"]")) < getd(.@Var$+"["+(.@i+1)+"]"))
					set .@nreq,1;
			}
			if (.@nreq)
				{cutin "",255; close;}
			next;
		}
		mes .n$;
		mes "---------------------------------";
		mes "您真的需要变更您的性别吗?";
		next;
		if(select("- 变更为 "+((Sex)?"女性":"男性")+":- 取消") == 1) {
			if (getarraysize(getd(.@Var$))) {
				for(set .@i,0; .@i<getarraysize(getd(.@Var$)); set .@i,.@i+2)
					delitem getd(.@Var$+"["+.@i+"]"), getd(.@Var$+"["+(.@i+1)+"]");
			}
			changesex;
		}
		cutin "",255; close;
	case 10:
		setarray .@Total[0],0,0;
		getinventorylist;
		mes .n$;
		mes "---------------------------------";
		for(set .@i,0; .@i<@inventorylist_count; set .@i,.@i+1) {
			if (@inventorylist_identify[.@i]) continue;
			if (Zeny < .Prices[3]) {
				if (!.@Total[0]) {
					mes "需要 "+Cm(.Prices[3])+" Zeny, 的鉴定费用来鉴定所有物品.";
					cutin "",255; close;
				}
				break;
			}
			set Zeny, Zeny-.Prices[3];
			delitem2 @inventorylist_id[.@i],1,0,0,0,0,0,0,0;
			getitem @inventorylist_id[.@i],1;
			setarray .@Total[0], .@Total[0]+1, .@Total[1]+.Prices[3];
		}
		if (!.@Total[0]) {
			mes "没有可鉴定的物品.";
			cutin "",255; close;
		}
		specialeffect2 EF_REFINEOK;
		mes "总共鉴定了 "+.@Total[0]+" 件物品"+((.@Total[1])?" 共需 "+Cm(.@Total[1])+" Zeny":"")+".";
		cutin "",255; close;

//	case 11:	
//		callfunc "Warper",4;
//		break;
	}
OnInit:

// --------------------- Config ---------------------
	setarray .MainMenu$[0],	// Set menu options!
		"职业辅助",		// [1]
		"造型",		// [2]
		"精炼",		// [4]
		"卡片拆除",		// [8]
		"硬币兑换",	// [16]
		"物品租赁",		// [32]
		"重置技能/属性点",	// [64]
		"修复技能",	// [128]
		"变性",		// [256]
		"全部鉴定";		// [512]
//		"传送";		// [1024]

	set .@MenuOption,1|2|8|16|32|64|128|512;

	setarray .OnClick[0],1,0,1;		// When clicked: heal [0], buff [1], repair [2] (1: yes / 0: no)
	setarray .Styles[1],			// Maximum dye, hair style, and hair color
		getbattleflag("max_cloth_color"),getbattleflag("max_hair_style"),getbattleflag("max_hair_color");
	setarray .Coin[0],674,1000000;	// Coin item ID, coin price
	setarray .Prices[0],1000000,400000,800,100;	// Zeny to remove cards [0], reset [1], rent [2], identify [3]

	setarray .Rebirth[0],99,50;	// Minimum base level, job level to rebirth OR change to third Class
	setarray .JobReq[0],10,40;	// Minimum job level to turn into 1st Class, 2nd Class
	set .ThirdClass,1;			// Enable third classes? (1: yes / 0: no)
	set .SNovice,45;			// Minimum base level to turn into Super Novice
	set .LastJob,1;				// Enforce linear Class changes? (1: yes / 0: no)
	set .SkillPointCheck,1;		// Force player to use up all skill points? (1: yes / 0: no)
	set .Platinum,1;			// Get platinum skills automatically? (1: yes / 0: no)

	set .EnrichedRefine,0;					// Allow enriched refiner option? (1: yes / 0: no)
	setarray .RP[0],2000,50,200,5000,20000;	// Zeny for armor, lv1, lv2, lv3, lv4 refines

	setarray .SexChangeF[0],2213,1,1558,1,7152,50;	// Items to turn into Female (ID,Count)
	setarray .SexChangeM[0],5016,1,2241,1,1017,50;	// Items to turn into Male (ID,Count)

	set .n$, "- ^cc6600孙燕姿^000000 - ^cccccc玩家辅助指导^000000";	// NPC Name
	waitingroom "玩家辅助指导",0;
// --------------------------------------------------
	set .Menu$,"";
	for (set .@i,0; .@i < getarraysize(.MainMenu$); set .@i,.@i+1) {
		if (.@MenuOption & (1<<.@i))
			set .Menu$, .Menu$+"- "+.MainMenu$[.@i];
		set .Menu$, .Menu$+":";
	}
	end;
	cutin "",255;
}

//"普隆德拉"
//prontera,146,92,6	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#1	4_F_SON
//"依斯鲁德岛"
izlude,134,100,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#2	4_F_SON
//"艾尔帕兰"
aldebaran,143,116,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#3	4_F_SON
//"吉芬"
geffen,122,62,8	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#4	4_F_SON
//"梦罗克"
morocc,160,97,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#5	4_F_SON
//"爱尔贝塔"
alberta,25,238,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#6	4_F_SON
//"斐扬"
payon,184,104,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#7	4_F_SON
//"尤诺"
yuno,164,48,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#8	4_F_SON
//"天津"
amatsu,192,81,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#9	4_F_SON
//"昆仑"
//gonryun,336,14,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#10	4_F_SON
//"姜饼城"
xmas,149,135,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#11	4_F_SON
//"洛阳"
louyang,224,60,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#12	4_F_SON
//"尼芙菲姆"
//niflheim,21,339,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#13	4_F_SON
//"夏威夷"
jawaii,223,204,4	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#14	4_F_SON
//"莫斯科"
moscovia,226,191,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#15	4_F_SON
//"伯仁斯"
veins,215,123,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#16	4_F_SON
//"艾音贝赫"
einbech,77,103,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#17	4_F_SON
//"拉赫"
rachel,134,99,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#18	4_F_SON
//"修吉尔"
hugel,99,168,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#19	4_F_SON
//"艾音布罗克"
einbroch,69,203,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#20	4_F_SON
//"里希塔勒镇"
lighthalzen,332,100,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#21	4_F_SON
//"克魔岛"
comodo,194,336,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#22	4_F_SON
//"汶巴拉"
umbala,90,339,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#23	4_F_SON
//"泰国"
ayothaya,145,117,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#24	4_F_SON
//"莫拉"
mora,133,103,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#25	4_F_SON
//"艾卡拉奇"
eclage,228,244,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#26	4_F_SON
//"绵绵岛"
malangdo,216,106,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#27	4_F_SON
//"德瓦塔"
dewata,204,186,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#28	4_F_SON
//"艾迪斯特斯"
dicastes01,194,190,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#29	4_F_SON
//"巴西利亚"
brasilis,222,97,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#30	4_F_SON
//"米德加尔特联合驻地"
mid_camp,211,242,5	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#31	4_F_SON
//"辉煌领域"
splendide,195,178,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#32	4_F_SON
//"马努克"
manuk,272,199,3	duplicate(孙燕姿 玩家辅助指导)	孙燕姿 玩家辅助指导#33	4_F_SON
